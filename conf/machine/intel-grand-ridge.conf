#
# Copyright (C) 2024 Wind River Systems, Inc.
#
#@TYPE: Machine
#@NAME: intel-grand-ridge
#@DESCRIPTION: Machine configuration for Intel Grand Ridge

DEFAULTTUNE ?= "grandridge-64"
require conf/machine/include/tune-grandridge.inc
require conf/machine/intel-x86.inc

# multilib support
MULTILIBS ?= "multilib:lib32"
DEFAULTTUNE:virtclass-multilib-lib32 ?= "grandridge-32"

SERIAL_CONSOLES = "115200;ttyS4"

# wks file for Intel Grand Ridge
WKS_FILE = "intel-grandridge-mkefidisk.wks.in"

# When using Intel Grand Ridge machine specific tune flags, some packages are
# not compatible with some x86 instruction sets in 32-bit multilib build.
TUNE_CCARGS:append:pn-lib32-rocksdb = "${@bb.utils.contains('TUNE_FEATURES', 'grandridge', ' -mno-avx -mno-avx2 -mno-bmi -mno-bmi2 -mno-pclmul', '', d)}"
TUNE_CCARGS:append:pn-lib32-grpc = "${@bb.utils.contains('TUNE_FEATURES', 'grandridge', ' -mno-bmi -mno-bmi2', '', d)}"
TUNE_CCARGS:append:pn-lib32-python3-grpcio = "${@bb.utils.contains('TUNE_FEATURES', 'grandridge', ' -mno-bmi -mno-bmi2', '', d)}"
TUNE_CCARGS:append:pn-lib32-python3-mldtypes = "${@bb.utils.contains('TUNE_FEATURES', 'grandridge', ' -mno-avx -mno-avx2', '', d)}"

# kexec-tools don't work with AVX, so disable it when using grandridge tune feature.
TUNE_CCARGS:append:pn-kexec-tools = "${@bb.utils.contains('TUNE_FEATURES', 'grandridge', ' -mno-avx -mno-avx2', '', d)}"
